# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=5.9.43
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL3')
depends=('at-spi2-core' 'deepin-qt5integration' 'deepin-qt-dbus-factory'
         'dtkwidget' 'libsecret' 'uchardet' 'libchardet')
makedepends=('cmake' 'expac' 'lxqt-build-tools' 'ninja' 'qt5-tools')
#checkdepends=('xorg-server-xvfb')
optdepends=('zssh: for remote management support')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-terminal/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f4d43f37637f415f0372039cc071ec34813333c45c452ad068d77bb33bf2fec116d8e59c9c520113c3d418a761afc0bdff06c1e03db48419e6f79ced3272d0a1')

prepare() {
  mkdir build
  cd deepin-terminal-$pkgver

  # ninja: error: '/build/deepin-terminal/src/deepin-terminal-5.4.0.6/default-config.json', needed by '/build/deepin-terminal/src/deepin-terminal-5.4.0.6/settings_translation.cpp', missing and no known rule to make it
  sed -i 's|default-config.json|src/assets/other/default-config.json|' CMakeLists.txt
}

build(){
  cd build
  # version missing in about dialog
  cmake -GNinja ../deepin-terminal-$pkgver #-DAPP_VERSION=$pkgver -DVERSION=$pkgver
  ninja
}

check() {
  cd build
  #LD_LIBRARY_PATH="$PWD/3rdparty/terminalwidget" xvfb-run ninja test || echo "Tests failed"
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
