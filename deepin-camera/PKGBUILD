# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-camera
pkgver=1.4.13
pkgrel=1
pkgdesc='Tool to view camera, take photo and video'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-camera"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'deepin-image-editor' 'ffmpeg'
         'ffmpegthumbnailer' 'portaudio' 'pulseaudio' 'qt5-multimedia')
makedepends=('cmake' 'deepin-gettext-tools' 'ninja' 'qt5-tools')
optdepends=('deepin-image-viewer: view camera photos'
            'deepin-movie: view camera records'
            'deepin-file-manager: open with other applications')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-camera/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6f652306d90298997c9718fec14556be05e975a3f20f970a273ce0a5625be220b3fa483911b6159d2ca5bfaed63dff3950b8b7847328308c2a459d743d8cc9f0')

build() {
  cd deepin-camera-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  ninja
}

package() {
  cd deepin-camera-$pkgver
  DESTDIR="$pkgdir" ninja install
}
