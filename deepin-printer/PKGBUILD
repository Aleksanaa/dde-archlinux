# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.9.19
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('cups' 'deepin-qt5integration' 'libusb' 'smbclient')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/dde-printer/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('559e6841d366f699039b0518ed26237cf5326f4ea85d8cb369d2e1b6eb3c9cbbf0aba7eb4701b2697838aa770d81166f3c3c1efeb475ff727408046713dbaf67')

prepare() {
  cd dde-printer-$pkgver
  sed -i '$aOnlyShowIn=Deepin' src/Printer/platform/linux/dde-printer.desktop

  # https://github.com/linuxdeepin/dde-printer/issues/4
  sed -i '/#include <strings.h>/a #include <stdexcept>' src/cppcups/cupssnmp.cpp
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
